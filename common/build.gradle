plugins {
    id 'java'
	id 'net.neoforged.moddev' version "${neoforge_gradle_version}"
}

sourceSets {
    stub
}

neoForge {
    neoFormVersion = neo_form_version

    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = project.minecraft_version
        mappingsVersion = project.parchment_version
    }
}

repositories {
    maven {
        name = 'Sponge'
        url = 'https://repo.spongepowered.org/repository/maven-public'
    }
}

dependencies {
    compileOnly "org.spongepowered:mixin:0.8.7"
    compileOnly "io.github.llamalad7:mixinextras-common:0.5.0"
    annotationProcessor "io.github.llamalad7:mixinextras-common:0.5.0"

    implementation "me.shedaniel.cloth:cloth-config-neoforge:${project.cloth_config_version}"
    compileOnly sourceSets.stub.output
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
    stub
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
    stub sourceSets.stub.output.toSet()
}
