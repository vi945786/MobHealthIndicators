plugins {
	id 'java'
}

subprojects {
    apply plugin: 'java'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
        }
        maven {
            name = "ParchmentMC"
            url = "https://maven.parchmentmc.org/"
        }
        maven { url "https://maven.shedaniel.me/" }
    }

    if (name == "common") return

    configurations {
        commonJava {
            canBeResolved = true
        }
        commonResources {
            canBeResolved = true
        }
    }

    dependencies {
        compileOnly project(':common')
        commonJava project(path: ':common', configuration: 'commonJava')
        commonResources project(path: ':common', configuration: 'commonResources')
        compileOnly project(path: ':common', configuration: 'stub')
    }

    tasks.named('compileJava', JavaCompile) {
        dependsOn(configurations.commonJava)
        source(configurations.commonJava)
    }

    base {
        archivesName = "${project.archives_base_name}-${project.mod_version}-${project.minecraft_version}-${project.name}"
    }

    jar {
         from(rootProject.file("LICENSE")) {
            rename { "${it}_${project.archives_base_name}"}
        }
    }

    processResources {
        dependsOn(configurations.commonResources)
        from(configurations.commonResources)

        inputs.property "version", project.mod_version

        filesMatching(["fabric.mod.json", "META-INF/neoforge.mods.toml"]) {
            expand "version": project.mod_version
        }
    }
}
