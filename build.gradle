plugins {
	id 'java'
}

configure(subprojects.findAll { it.name != "common" }) {
    if (name == "common") return

    apply plugin: 'java'

    version = "${project.mod_version}-${project.minecraft_version}-${project.name}"
    base.archivesName = project.archives_base_name

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
        }
        maven {
            name = "ParchmentMC"
            url = "https://maven.parchmentmc.org/"
        }
        maven { url "https://maven.shedaniel.me/" }
    }

    configurations {
        commonJava {
            canBeResolved = true
        }
        commonResources {
            canBeResolved = true
        }
    }

    dependencies {
        implementation(project(':common'))
        commonJava project(path: ':common', configuration: 'commonJava')
        commonResources project(path: ':common', configuration: 'commonResources')
        compileOnly project(path: ':common', configuration: 'stub')
    }

    tasks.named('compileJava', JavaCompile) {
        source(configurations.commonJava)
    }

    jar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${project.archivesBaseName}"}
        }
    }

    processResources {
        from(configurations.commonResources)

        inputs.property "version", project.mod_version

        filesMatching("fabric.mod.json") {
            expand "version": project.mod_version
        }
    }
}